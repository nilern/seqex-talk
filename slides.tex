\documentclass{beamer}

\usepackage[utf8]{inputenc}

\title{Structure and Interpretation of Malli Regex Schemas}
\author{Pauli Jaakkola}
\date{22.6.2021}

\begin{document}

\frame{\titlepage}

%%%

\begin{frame}

\begin{itemize}
\item The empty string \texttt{\#""} or sequence \texttt{[:cat]}
\item Literal character \texttt{\#"a"} or element schema \texttt{:int}
\item Concatenation \texttt{\#"ab"} or \texttt{[:cat :string :int]}
\item Alternation \texttt{\#"a|b"} or \texttt{[:alt :string :int]}
\item Kleene Star \texttt{\#"a*"} or \texttt{[:* :int]}
\end{itemize}

also

\begin{itemize}
\item Optional \texttt{\#"a?"} or \texttt{[:? :int]}
\item Kleene Plus \texttt{\#"a+"} or \texttt{[:+ :int]}
\item Repeat \texttt{\#"a\{1, 5\}"} or \texttt{[:repeat \{:min 1, :max 5\} :int]}
\end{itemize}

\end{frame}

%%%

\begin{frame}[fragile]

{\scriptsize
\begin{semiverbatim}
(m/parse
  [:schema {:registry {"hiccup" [:or
                                 [:catn
                                  [:name keyword?]
                                  [:props [:? [:map-of keyword? any?]]]
                                  [:children [:* [:schema [:ref "hiccup"]]]]]
                                 [:or nil? boolean? number? string?]]}}
   "hiccup"]
  [:div {:class [:foo :bar]}
   "Hello, world of data"])
;=> {:name :div, :props {:class [:foo :bar]}, :children ["Hello, world of data"]}
\end{semiverbatim}
}

\end{frame}

%%%

\begin{frame}[fragile]

{\scriptsize
\begin{semiverbatim}
(require '[clojure.spec.alpha :as s])

(s/def ::ws (s/* \#(Character/isWhitespace \%)))

(s/def ::sexpr
  (s/cat
    :lws ::ws
    :sexpr (s/alt
             :list (s/cat :lparen \#(= \\( \%)
                          :sexprs (s/* ::sexpr)
                          :rparen #(= \\) \%))
             :symbol (s/cat :c #(Character/isJavaIdentifierStart \%)
                            :cs (s/* #(Character/isJavaIdentifierPart \%)))
             :int (s/+ \#(Character/isDigit \\\}%)))
    :rws ::ws))

(s/valid? ::sexpr (seq "(foo (bar 1 2))"))
; => true

(s/valid? ::sexpr (seq "(foo (bar 1 2)"))
; => false
\end{semiverbatim}
}

\end{frame}

%%%

\begin{frame}

\begin{itemize}
\item \textsc{dfa} (\texttt{grep}, Lex)
\item \textsc{nfa} (Seqexp)
\item Backtracking (\textsc{pcre})
\item Dynamic derivatives (Spec)
\end{itemize}

\end{frame}

%%%

\begin{frame}

\begin{center}
\begin{tabular}{c | c c}
Algorithm & Time usage & Space usage \\
\hline
\textsc{dfa} & \(O(n)\) & \(O(1)\) \\
\textsc{nfa} & \(O(rn)\) & \(O(r)\) \\
Backtracking & \(O(2^n)\) & \(O(n)\) \\
Memoized backtracking & \(O(n)\) & \(O(rn)\) \\
Dynamic derivatives & \(O(rn)\) & \(O(n^r)\)
\end{tabular}
\end{center}

\end{frame}

%%%

\begin{frame}[fragile]

\begin{semiverbatim}
(defn item-parser [parse-item]
  (fn [coll]
    (if (seq coll)
      (let [res (parse-item (first coll))]
        (if-not (miu/-invalid? res)
          [res (rest coll)]
          res))
      :malli.core/invalid)))
\end{semiverbatim}

\begin{semiverbatim}
(defn end-parser [coll]
  (if (empty? coll)
    [nil coll]
    :malli.core/invalid))
\end{semiverbatim}

\end{frame}

%%%

\begin{frame}[fragile]

\begin{semiverbatim}
(defn pure-parser [v] (fn [coll] [v coll]))
\end{semiverbatim}

\begin{semiverbatim}
(defn cat-parser [& parsers]
  (reduce (fn [parser parser*]
            (fn [coll]
              (let [res (parser coll)]
                (if-not (miu/-invalid? res)
                  (let [[vs coll] res]
                    (let [res* (parser* coll)]
                      (if-not (miu/-invalid? res*)
                        (let [[v coll] res*]
                          [(conj vs v) coll])
                        res*)))
                  res))))
          (pure-parser []) parsers))
\end{semiverbatim}

\end{frame}

%%%

\begin{frame}[fragile]

\begin{semiverbatim}
(defn alt-parser [& parsers]
  (reduce (fn [parser parser*]
            (fn [coll]
              (let [res (parser coll)]
                (if-not (miu/-invalid? res)
                  res
                  (parser* coll)))))
          parsers))
\end{semiverbatim}

\end{frame}

%%%

\begin{frame}[fragile]

\begin{semiverbatim}
(defn *-parser [parser]
  (letfn [(parser* [vs coll]
            (let [res (parser coll)]
              (if-not (miu/-invalid? res)
                (let [[v coll] res]
                  (recur (conj vs v) coll))
                [vs coll])))]
    (fn [coll] (parser* [] coll))))
\end{semiverbatim}

\end{frame}

%%%

\begin{frame}[fragile]

\begin{semiverbatim}
(defn ?-parser [parser]
  (alt-parser parser (pure-parser nil)))
\end{semiverbatim}

\begin{semiverbatim}
(defn +-parser [parser]
  (map-parser (fn [[v vs]] (cons v vs))
              (cat-parser parser (*-parser parser))))
\end{semiverbatim}

\end{frame}

%%%

\begin{frame}[fragile]

\begin{semiverbatim}
(defn map-parser [f parser]
  (fn [coll]
    (let [res (parser coll)]
      (if-not (miu/-invalid? res)
        (let [[v coll] res]
          [(f v) coll])
        res))))
\end{semiverbatim}

\end{frame}

%%%

\begin{frame}[fragile]

{\scriptsize
\begin{semiverbatim}
(defn repeat-parser [min max parser]
  (letfn [(compulsories [n vs coll]
            (if (< n min)
              (let [res (parser coll)]
                (if-not (miu/-invalid? res)
                  (let [[v coll] res]
                    (recur (inc n) (conj vs v) coll))
                  res))
              (optionals n vs coll)))

          (optionals [n vs coll]
            (if (< n max)
              (let [res (parser coll)]
                (if-not (miu/-invalid? res)
                  (let [[v coll] res]
                    (recur (inc n) (conj vs v) coll))
                  [vs coll]))
              [vs coll])]
    (fn [coll] (compulsories 0 [] coll)))))
\end{semiverbatim}
}

\end{frame}

%%%

\begin{frame}[fragile]

{\scriptsize
\begin{semiverbatim}
(m/parse [:cat [:* pos?] [:= 4]] [2 4])
\end{semiverbatim}

\begin{semiverbatim}
(let [parse (cat-parser (*-parser (item-parser (m/parser pos?)))
                        (item-parser (m/parser [:= 4])))]
  (parse [2 4]))
\end{semiverbatim}
}

\end{frame}

%%%

\begin{frame}[fragile]

{\scriptsize
\begin{semiverbatim}
(defn *-parser [parser]
  (letfn [(epsilon (fn [_ vs coll k] (k vs coll)))

          (parser* [stack vs coll k]
            (park*! stack epsilon vs coll k) ; remember fallback
            (parser stack coll
                    (fn [v coll]
                      (park*! stack parser* (conj vs v) coll k))))] ; TCO
    (fn [stack coll k] (parser* stack [] coll k))))
\end{semiverbatim}
}

\end{frame}

%%%

\begin{frame}[fragile]

{\scriptsize
\begin{semiverbatim}
(defn repeat-parser [min max parser]
  (letfn [(epsilon (fn [_ vs coll k] (k vs coll)))

          (compulsories [stack n vs coll k]
            (if (< n min)
              (parser stack coll
                      (fn [v coll]
                        (park**! stack compulsories
                                 (inc n) (conj vs v) coll k))) ; TCO
              (optionals stack n vs coll k)))

          (optionals [stack n vs coll k]
            (if (< n max)
              (do
                (park*! stack epsilon vs coll k) ; remember fallback
                (parser stack coll
                        (fn [v coll]
                          (park**! stack optionals
                                   (inc n) (conj vs v) coll k)))) ; TCO
              (k vs coll)))]
    (fn [stack coll k] (compulsories stack 0 [] coll k))))
\end{semiverbatim}
}

\end{frame}

%%%

\begin{frame}[fragile]

{\scriptsize
\begin{semiverbatim}
(defn alt-parser [& parsers]
  (reduce (fn [parser parser*]
            (fn [stack coll k]
              (park! stack parser* coll k) ; remember fallback
              (parser stack coll k)))
          parsers))
\end{semiverbatim}
}

\end{frame}

%%%

\begin{frame}[fragile]

\begin{semiverbatim}
(defn item-parser [parse-item]
  (fn [_ coll k]
    (when (seq coll)
      (let [res (parse-item (first coll))]
        (when-not (miu/-invalid? res)
          (k res (rest coll)))))))
\end{semiverbatim}

\begin{semiverbatim}
(defn end-parser [_ coll k]
  (when (empty? coll)
    (k nil coll)))
\end{semiverbatim}

\end{frame}

%%%

\begin{frame}[fragile]

{\scriptsize
\begin{semiverbatim}
(defn cat-parser [& parsers]
  (let [p (reduce (fn [rest-parser parser]
                    (fn [stack vs coll k]
                      (parser stack coll
                              (fn [v coll]
                                (rest-parser stack (conj vs v) coll k)))))
                  (reverse parsers))]
    (fn [stack coll k] (p stack [] coll k))))
\end{semiverbatim}
}

\end{frame}

%%%

\begin{frame}[fragile]

\begin{semiverbatim}
(defn pure-parser [v] (fn [_ coll k] (k v coll)))
\end{semiverbatim}

\begin{semiverbatim}
(defn map-parser [f parser]
  (fn [stack coll k]
    (parser stack coll
            (fn [v coll] (k (f v) coll)))))
\end{semiverbatim}

\end{frame}

%%%

\begin{frame}[fragile]

{\scriptsize
\begin{semiverbatim}
(defn parser [p]
  (let [p (cat-parser p (end-parser))]
    (fn [coll]
      (if (sequential? coll)
        (let [stack (make-stack)
              success (volatile! false)
              result (volatile! nil)]
          (p stack coll (fn [v _ _]
                          (vreset! success true)
                          (vreset! result v)))
          (if @success
            (first @result)
            (loop []
              (if-some [thunk (pop-thunk! stack)]
                (do
                  (thunk)
                  (if @success
                    (first @result)
                    (recur)))
                :malli.core/invalid))))
        :malli.core/invalid))))
\end{semiverbatim}
}

\end{frame}

%%%

\begin{frame}[fragile]

\begin{semiverbatim}
(defn make-stack []
  #?(:clj (ArrayDeque.)
     :cljs #js []))

(defn- empty-stack? [^ArrayDeque stack]
  #?(:clj (.isEmpty stack)
     :cljs (zero? (alength stack))))

(defn park! [^ArrayDeque stack parser coll k]
  (.push stack #(parser stack coll k)))

(defn pop-thunk! [^ArrayDeque stack]
  (when-not (empty-stack? stack) (.pop stack)))
\end{semiverbatim}

\end{frame}

\end{document}

